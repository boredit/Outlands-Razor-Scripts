#example: make cure potions, fill a keg, drop the keg on your shelf.
###Instructions###
#1.)configure shelf profile to pull garlic bottles and mortars
#2.)place empty keg in backpack
#3.)on the Scripts>Options tab create a variable named 'empty_keg' and target the keg
#4.)on the Scripts>Options tab create a variable named 'shelf' and target your shelf
#5.)stand in front of shelf and make cure potions
###Instructions###

###Begin###
#check for mortars
if not findtype 'mortar and pestle'
    overhead 'Out of tools!'
    menu shelf 1
endif
#check for garlic | count uses the #s from the Display/Counter tab
if count 'garlic' < 6
    overhead 'Out of reagent!'
    menu shelf 1
endif
#check for bottles
if not findtype '3854' 'backpack'
    overhead 'Grabbing bottles!'
    menu shelf 1
endif
#stop if a society job is completed
if insysmsg 'have completed'
    overhead 'Society job completed!' 88
    #drop the remaining potions in your shelf
    lifttype 'keg' 1
    drop 'shelf' -1 -1 0
    overhead 'keg dropped'
    stop
endif
#drops a pot on the keg to get started
if findtype 'Orange Potion' true
    lifttype 'Orange Potion' 1
    drop 'empty_keg' -1 -1 0
    wait 500
endif
#drop keg on shelf when full
if insysmsg 'not hold any more'
    overhead 'Stocking shelf...
    lifttype 'keg' 1
    drop 'shelf' -1 -1 0
    overhead 'keg dropped'
    wait 500
endif
#make potion
dclicktype 'mortar and pestle'
waitforgump 949095101
gumpresponse 101
waitforgump 949095101
gumpresponse 202
waitforgump 949095101
waitforgump 949095101
pause 2000
gumpclose
#Uhh... loop.
loop
